buildscript {
    repositories {
        jcenter()
        maven { url 'https://www.dimdev.org/maven/' }
        maven { url 'http://repo.spongepowered.org/maven' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'org.dimdev:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.1.0'
    }
}

apply plugin: 'net.minecraftforge.gradle.tweaker-client'
apply plugin: 'java'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.matthewprenger.cursegradle'

group 'yalter.mousetweaks'
version '0.1.4'
archivesBaseName = 'MouseTweaksForRift'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://www.dimdev.org/maven/' }
}

dependencies {
    implementation 'org.dimdev:rift:1.0.2-33:dev'
}

minecraft {
    version = '1.13'
    mappings = 'snapshot_5'
    runDir = 'run'
    tweakClass = 'org.dimdev.riftloader.launch.RiftLoaderTweaker'
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, 'mixins.mousetweaks.refmap.json'
}

jar {
    from sourceSets.api.output
    archiveName = "${project.archivesBaseName}-${version}-mc${project.minecraft.version}.jar"
}

sourceJar {
    from sourceSets.api.allSource
    archiveName = "${project.archivesBaseName}-${version}-mc${project.minecraft.version}-src.jar"
}

task apiJar(type: Jar) {
    from sourceSets.api.output
    archiveName = "${project.archivesBaseName}-${version}-mc${project.minecraft.version}-api.jar"
}

artifacts {
    archives apiJar
}

curseforge {
    apiKey = System.getenv('CURSEFORGE_KEY') ?: '0'
    project {
        id = '299294'
        releaseType = 'beta'
        addGameVersion "${project.minecraft.version}"

        mainArtifact(jar) {
            displayName = "[${project.minecraft.version}] Mouse Tweaks for Rift ${version}"
            relations {
                requiredDependency 'rift'
            }
        }
        addArtifact(apiJar) {
            displayName = 'API JAR'
        }
        addArtifact(sourceJar) {
            displayName = 'Source JAR'
        }
    }
}
